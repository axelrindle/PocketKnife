plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.31"
    id "maven"
    id "jacoco"
}

group = "com.github.axelrindle"
version = "1.2.1"

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
        html.enabled = false
    }
}

configurations {
    testCompile.extendsFrom compileOnly
}

dependencies {
    // Kotlin stdlib
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.31"
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:1.3.31"

    // Dependencies
    compile group: 'org.jetbrains', name: 'annotations', version: '16.0.3'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'

    // Spigot/Bukkit API
    compileOnly "org.spigotmc:spigot-api:1.14.1-R0.1-SNAPSHOT"

    // Tests
    testCompile project
    testCompile 'junit:junit:4.12'
    testCompile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'io.kotlintest:kotlintest-runner-junit5:3.1.10'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
}

processResources {
    filter { line ->
        line.replaceAll("@VERSION@", project.version.toString())
    }
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}
