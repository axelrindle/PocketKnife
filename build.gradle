plugins {
    id "org.jetbrains.kotlin.jvm" version "$version_kotlin"
    id "com.eden.orchidPlugin"
}

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

dependencies {
    orchidRuntimeOnly "io.github.javaeden.orchid:OrchidCore:$version_orchid"
    orchidRuntimeOnly "io.github.javaeden.orchid:OrchidDocs:$version_orchid"
    orchidRuntimeOnly "io.github.javaeden.orchid:OrchidKotlindoc:$version_orchid"
    orchidRuntimeOnly "io.github.javaeden.orchid:OrchidPluginDocs:$version_orchid"
    orchidRuntimeOnly "io.github.javaeden.orchid:OrchidGithub:$version_orchid"
    orchidRuntimeOnly "io.github.javaeden.orchid:OrchidSyntaxHighlighter:$version_orchid"
    orchidRuntimeOnly "io.github.javaeden.orchid:OrchidSearch:$version_orchid"
}

allprojects {
    version = project.version_plugin
    group = "de.axelrindle"

    repositories {
        mavenCentral()
        maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    apply plugin: "org.jetbrains.kotlin.jvm"

    compileKotlin {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
    }

    processResources {
        filter { line ->
            line.replaceAll("@VERSION@", rootProject.version.toString())
        }
    }

    task copyToServer(type: Copy) {
        def dir = System.getenv("PLUGINS_DIR")
        if (dir == null) return true

        from("$buildDir/libs") {
            include "*-${rootProject.version}.jar" // TODO: Only include archivesBaseName
        }
        into dir
    }
}

orchid {
    theme = "Editorial"
    version = "$project.version"
    srcDir = "orchid"
    destDir = "docs"
    baseUrl = "https://axelrindle.github.io/PocketKnife/"
    githubToken = System.getenv("GITHUB_TOKEN")
}