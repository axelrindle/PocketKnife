plugins {
    id "maven"
    id "jacoco"
    id "maven-publish"
}

archivesBaseName = "PocketKnifeAPI"

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
        html.enabled = false
    }
}

configurations {
    testCompile.extendsFrom compileOnly
}

dependencies {
    // Kotlin stdlib
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Spigot/Bukkit API
    compileOnly "org.spigotmc:spigot-api:$spigot_version"

    // Dependencies
    implementation group: "org.jetbrains", name: "annotations", version: "16.0.3"
    implementation group: "commons-io", name: "commons-io", version: "2.6"

    // Tests
    testImplementation "junit:junit:4.12"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-core-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-property-jvm:$kotest_version"
    testImplementation group: "org.slf4j", name: "slf4j-simple", version: "1.7.25"
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.16:0.25.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/axelrindle/PocketKnife")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
}