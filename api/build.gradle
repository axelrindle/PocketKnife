plugins {
    id "jacoco"
    id "maven-publish"
}

group = "com.github.axelrindle"
archivesBaseName = "PocketKnife"

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
        html.enabled = false
    }
}

dependencies {
    // Kotlin stdlib
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    // Spigot/Bukkit API
    compileOnly "org.spigotmc:spigot-api:$version_spigot"

    // Dependencies
    implementation group: "org.jetbrains", name: "annotations", version: "16.0.3"
    implementation group: "commons-io", name: "commons-io", version: "2.6"

    // Tests
    testImplementation "io.kotest:kotest-runner-junit5:$version_kotest"
    testImplementation "io.kotest:kotest-assertions-core-jvm:$version_kotest"
    testImplementation group: "org.slf4j", name: "slf4j-simple", version: "1.7.25"
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.16:0.31.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenKotlin(MavenPublication) {
            artifactId = 'PocketKnife'
            from components.java
            pom {
                name = 'PocketKnife'
                description = 'Utilities and reuseable components to simplify the process of creating a Bukkit/Spigot plugin.'
                url = 'https://github.com/axelrindle/PocketKnife'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/axelrindle/PocketKnife/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'axelrindle'
                        name = 'Axel Rindle'
                        email = 'mail@axelrindle.de'
                    }
                }
            }
        }
    }
}